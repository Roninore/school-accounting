
&НаКлиенте
Процедура КомандаВыполнитьЗапрос(Команда)
	
	НеотформатированныйТекст = ТекстЗапроса.ПолучитьТекст();
	ОтформатированныйТекст = СтрЗаменить(НеотформатированныйТекст, "|", "");
	
	Если ПустаяСтрока(ОтформатированныйТекст) Тогда
		ТекстПредупреждения = НСтр("ru = 'Введите текст запроса.'");
		ПоказатьПредупреждение(,ТекстПредупреждения);
	КонецЕсли;
	
	ТекстЗапроса.УстановитьТекст(ОтформатированныйТекст);
	
	РезультатВыполненияЗапроса = Новый ТабличныйДокумент;
	
	ВыполнитьЗапрос(ТекстЗапроса.ПолучитьТекст());
	
	Элементы.ГруппаСтраниц.ТекущаяСтраница = Элементы.Запрос;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьЗапрос(ПолученныйТекстЗапроса)
	Запрос = Новый Запрос;
	Запрос.Текст = ПолученныйТекстЗапроса;
	
	Запрос.УстановитьПараметр("ТЧ",Отчет.ТЧ_ТабличнаяЧасть.Выгрузить(,"ТЧ_Ссылка"));
	Запрос.УстановитьПараметр("НачалоПериода",Отчет.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",Отчет.КонецПериода);
	Запрос.УстановитьПараметр("ЗначениеСсылка",Отчет.ЗначениеСсылка);
	Запрос.УстановитьПараметр("ЗначениеЧисло",Отчет.ЗначениеЧисло);
	Запрос.УстановитьПараметр("ЗначениеБулево",Отчет.ЗначениеБулево);
	Запрос.УстановитьПараметр("ЗначениеСтрока",Отчет.ЗначениеСтрока);
	
		РезультатЗапроса = Запрос.Выполнить();
	РезультатЗапросаТЗ = РезультатЗапроса.Выгрузить();
	
	ТабДок = РезультатВыполненияЗапроса;
	ОбластьШапкаТаблицы = ТабДок.ПолучитьОбласть(1,1,1,РезультатЗапросаТЗ.Колонки.Количество());
	ОбластьДанныеТаблицы = ТабДок.ПолучитьОбласть(2,1,2,РезультатЗапросаТЗ.Колонки.Количество());	
	
	ЗаголовкиКолонокТаблицы = Новый Структура;
	
	ЛинияОбводки = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
	ШрифтЗаголовкаТаблицы = Новый Шрифт(, 8, Истина, , , );
	ШрифтДанныхТаблицы = Новый Шрифт(,8,,,,);
	
	ЦветФонаШапкиТаблицы = Новый Цвет(255,255,153);
	ЦветФонаДанныхТаблицы = Новый Цвет(245,255,250);
	
	Для Счетчик = 1 по РезультатЗапросаТЗ.Колонки.Количество() Цикл
		
		Колонка = РезультатЗапросаТЗ.Колонки[Счетчик-1];
		ЗаголовкиКолонокТаблицы.Вставить(Колонка.Имя, Колонка.имя);
		
		ОбластьЗаполненияШапки = ОбластьШапкаТаблицы.Область(1,Счетчик,1,Счетчик);
		ОбластьЗаполненияШапки.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		ОбластьЗаполненияШапки.ШиринаКолонки = 15;
		ОбластьЗаполненияШапки.Обвести(ЛинияОбводки,ЛинияОбводки,ЛинияОбводки,ЛинияОбводки);
		ОбластьЗаполненияШапки.Параметр = Колонка.Имя;
		ОбластьЗаполненияШапки.Шрифт = ШрифтЗаголовкаТаблицы;
		ОбластьЗаполненияШапки.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
		ОбластьЗаполненияШапки.ЦветФона = ЦветФонаШапкиТаблицы;
		
		ОбластьЗаполненияДанных = ОбластьДанныеТаблицы.Область(1,Счетчик,1,Счетчик);
		ОбластьЗаполненияДанных.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		ОбластьЗаполненияДанных.ШиринаКолонки = 15;
		ОбластьЗаполненияДанных.Обвести(ЛинияОбводки,ЛинияОбводки,ЛинияОбводки,ЛинияОбводки);
		ОбластьЗаполненияДанных.Параметр = Колонка.Имя;
		ОбластьЗаполненияДанных.Шрифт = ШрифтДанныхТаблицы;
		ОбластьЗаполненияДанных.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
		ОбластьЗаполненияДанных.ЦветФона = ЦветФонаДанныхТаблицы;                     
		
	КонецЦикла;
	                       
	ОбластьШапкаТаблицы.Параметры.Заполнить(ЗаголовкиКолонокТаблицы);
	ТабДок.Вывести(ОбластьШапкаТаблицы);
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ОбластьДанныеТаблицы.Параметры.Заполнить(Выборка);
		ТабДок.Вывести(ОбластьДанныеТаблицы);
		
	КонецЦикла;
	
КонецПроцедуры
		